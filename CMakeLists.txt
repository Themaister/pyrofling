cmake_minimum_required(VERSION 3.10)
set(CMAKE_CXX_STANDARD 14)
set(CMAKE_C_STANDARD 99)
project(pyrofling LANGUAGES CXX C)

if (CMAKE_COMPILER_IS_GNUCXX OR (${CMAKE_CXX_COMPILER_ID} MATCHES "Clang"))
    set(PYROFLING_CXX_FLAGS -Wshadow -Wall -Wextra -Wno-comment -Wno-missing-field-initializers -Wno-empty-body -fvisibility=hidden)
    if (${CMAKE_CXX_COMPILER_ID} MATCHES "Clang")
        set(PYROFLING_CXX_FLAGS ${PYROFLING_CXX_FLAGS} -Wno-backslash-newline-escape)
    endif()
    if (NOT (${CMAKE_BUILD_TYPE} MATCHES "Release"))
        message("Enabling frame pointer for profiling/debug.")
        set(PYROFLING_CXX_FLAGS ${PYROFLING_CXX_FLAGS} -fno-omit-frame-pointer)
    endif()
    if (CMAKE_SYSTEM_PROCESSOR MATCHES "(x86)|(X86)|(amd64)|(AMD64)")
        message("Enabling SSE3 support.")
        set(PYROFLING_CXX_FLAGS ${PYROFLING_CXX_FLAGS} -msse3)
    endif()
elseif (MSVC)
    set(PYROFLING_CXX_FLAGS /D_CRT_SECURE_NO_WARNINGS /wd4267 /wd4244 /wd4309 /wd4005 /MP)
endif()

option(PYROFLING_LAYER_ONLY "Only build capture layer." OFF)

include(GNUInstallDirs)

if (NOT WIN32)
    add_subdirectory(ipc)
endif()

# Disable stuff we don't need. We'll use slangmosh to precompile the two shaders we care about.
set(GRANITE_VULKAN_SYSTEM_HANDLES ON CACHE BOOL "" FORCE)
set(GRANITE_RENDERER ON CACHE BOOL "" FORCE)
set(GRANITE_VULKAN_SHADER_COMPILER_OPTIMIZE OFF CACHE BOOL "" FORCE)
set(GRANITE_VULKAN_SHADER_MANAGER_RUNTIME_COMPILER OFF CACHE BOOL "" FORCE)
set(GRANITE_VULKAN_FOSSILIZE OFF CACHE BOOL "" FORCE)
set(GRANITE_VULKAN_SPIRV_CROSS OFF CACHE BOOL "" FORCE)
set(GRANITE_FFMPEG_VULKAN ON CACHE BOOL "" FORCE)

if (WIN32 AND (NOT MSVC) AND (IS_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/ffmpeg-build-msys2/output/lib))
    message("Found pre-built FFmpeg.")
    set(CMAKE_PREFIX_PATH "${CMAKE_CURRENT_SOURCE_DIR}/ffmpeg-build-msys2/output")
endif()

if (${CMAKE_BUILD_TYPE} MATCHES "Release")
    set(GRANITE_SHIPPING ON CACHE BOOL "" FORCE)
    message("Using GRANITE_SHIPPING build.")
else()
    set(GRANITE_SHIPPING OFF CACHE BOOL "" FORCE)
    message("Not using GRANITE_SHIPPING build.")
endif()

if (NOT PYROFLING_LAYER_ONLY)
    set(GRANITE_FFMPEG ON CACHE BOOL "" FORCE)
    set(GRANITE_AUDIO ON CACHE BOOL "" FORCE)
endif()

add_library(granite-application-interface-query STATIC pyrofling_app_query.cpp)

add_subdirectory(Granite EXCLUDE_FROM_ALL)

if (GRANITE_SANITIZE_ADDRESS)
    set(PYROFLING_CXX_FLAGS ${PYROFLING_CXX_FLAGS} -fsanitize=address)
    set(PYROFLING_LINK_FLAGS "${PYROFLING_LINK_FLAGS} -fsanitize=address")
endif()

add_subdirectory(proto)

add_library(pyrofling-socket STATIC simple_socket.cpp simple_socket.hpp)
target_include_directories(pyrofling-socket PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})
target_compile_options(pyrofling-socket PRIVATE ${PYROFLING_CXX_FLAGS})
target_link_libraries(pyrofling-socket PRIVATE pyro-protocol)

add_subdirectory(lt EXCLUDE_FROM_ALL)

if (NOT WIN32)
    add_subdirectory(pyro-server)
endif()
add_subdirectory(pyro-client)

if (NOT PYROFLING_LAYER_ONLY)
    if (NOT WIN32)
        add_subdirectory(examples)
        add_executable(pyrofling pyrofling.cpp virtual_gamepad.cpp virtual_gamepad.hpp)
        target_compile_options(pyrofling PRIVATE ${PYROFLING_CXX_FLAGS})
        target_link_libraries(pyrofling PRIVATE
                pyro-protocol pyrofling-ipc granite-threading granite-vulkan granite-video granite-audio pyro-server)
        install(TARGETS pyrofling)
        set_target_properties(pyrofling PROPERTIES LINK_FLAGS "${PYROFLING_LINK_FLAGS}")
    endif()

    include(FindPkgConfig)
    pkg_check_modules(PIPEWIRE IMPORTED_TARGET libpipewire-0.3)
    if (PIPEWIRE_FOUND)
        message("Found pipewire 0.3. Including PW node support.")
        target_link_libraries(pyrofling PRIVATE PkgConfig::PIPEWIRE)
        target_compile_definitions(pyrofling PRIVATE HAVE_PIPEWIRE)
    else()
        message("Did not find pipewire support.")
    endif()

	add_blob_archive_target(viewer-fonts viewer_fonts
        ${CMAKE_CURRENT_SOURCE_DIR}/Granite/assets/fonts/font.ttf fonts/font.ttf)

    add_granite_application(pyrofling-viewer pyrofling_viewer.cpp)
    target_link_libraries(pyrofling-viewer PRIVATE granite-video granite-audio pyro-client viewer-fonts)
    if (WIN32)
            target_link_libraries(pyrofling-viewer PRIVATE winmm)
    endif()
    set_target_properties(pyrofling-viewer PROPERTIES LINK_FLAGS "${PYROFLING_LINK_FLAGS}")
    install(TARGETS pyrofling-viewer)

    add_granite_application(pyrofling-latency-tester pyrofling_latency_tester.cpp)
    target_link_libraries(pyrofling-latency-tester PRIVATE granite-audio viewer-fonts)
    set_target_properties(pyrofling-latency-tester PROPERTIES LINK_FLAGS "${PYROFLING_LINK_FLAGS}")
    install(TARGETS pyrofling-latency-tester)

    add_granite_offline_tool(pyrofling-gamepad pyrofling_gamepad.cpp)
    target_link_libraries(pyrofling-gamepad PRIVATE pyro-client SDL3-static granite-input-sdl)
    if (WIN32)
        target_link_libraries(pyrofling-gamepad PRIVATE winmm)
    endif()
    install(TARGETS pyrofling-gamepad)
endif()

if (NOT WIN32)
	add_subdirectory(layer-util)
	add_subdirectory(capture-layer)
	add_subdirectory(cross-wsi-layer)
endif()
